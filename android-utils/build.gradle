apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
	publishedGroupId = 'com.github.dyvoker'
	artifact = 'android-utils'
	libraryVersion = '0.1.1'

	libraryName = 'Android Utils'
	libraryDescription = 'Utils for Android.'
	developerId = 'dyvoker'
	developerName = 'Ivan Tormozov'
	developerEmail = 'dyvoker@gmail.com'
	siteUrl = 'https://github.com/dyvoker/android-utils'
	gitUrl = 'https://github.com/dyvoker/android-utils.git'
	bintrayRepo = 'maven'
	bintrayName = 'android-utils'

	licenseName = 'The Apache Software License, Version 2.0'
	licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
	allLicenses = ["Apache-2.0"]
}

group publishedGroupId
version libraryVersion

android {
	compileSdkVersion 27

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 27
		versionCode 6
		versionName libraryVersion
	}
	buildTypes {
		release {
			minifyEnabled false
		}
	}

	resourcePrefix 'andr_ut_'
}

dependencies {
	api 'com.android.support:support-annotations:27.1.1'
}


task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// Add all dependency to the javadoc.classpath (avoid "error: cannot find symbol").
afterEvaluate {
	javadoc.classpath += files(android.libraryVariants.collect { variant ->
		variant.javaCompile.classpath.files
	})
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.getDestinationDir()
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

install {
	repositories.mavenInstaller {
		pom.project {
			packaging 'aar'
			groupId publishedGroupId
			artifactId artifact

			name libraryName
			description libraryDescription
			url siteUrl

			licenses {
				license {
					name licenseName
					url licenseUrl
				}
			}

			developers {
				developer {
					id developerId
					name developerName
					email developerEmail
				}
			}

			scm {
				connection gitUrl
				developerConnection gitUrl
				url siteUrl
			}
		}
	}
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	user = properties.getProperty('bintray.user')
	key = properties.getProperty('bintray.apikey')
	configurations = ['archives']

	pkg {
		repo = bintrayRepo
		name = bintrayName
		desc = libraryDescription
		websiteUrl = siteUrl
		vcsUrl = gitUrl
		licenses = allLicenses
		publish = true
		publicDownloadNumbers = true

		version {
			desc = libraryDescription
			vcsTag = 'v' + libraryVersion
		}
	}
}

// gradlew :android-utils:clean :android-utils:install :android-utils:bintrayUpload
